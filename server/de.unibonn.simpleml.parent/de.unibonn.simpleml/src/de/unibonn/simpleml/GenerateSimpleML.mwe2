module de.unibonn.simpleml.GenerateSimpleML

import org.eclipse.xtext.xtext.generator.*
import org.eclipse.xtext.xtext.generator.model.project.*

var rootPath = ".."

Workflow {
	bean = org.eclipse.emf.mwe.utils.StandaloneSetup {
        scanClassPath = true
        platformUri = rootPath
    }
    
    component = org.eclipse.emf.mwe.utils.DirectoryCleaner {
    	directory = "emf-gen"
    }
	
	component = org.eclipse.emf.mwe2.ecore.EcoreGenerator {
        genModel = "platform:/resource/de.unibonn.simpleml/model/custom/SimpleML.genmodel"
        srcPath = "platform:/resource/de.unibonn.simpleml/src"
    }
	
	component = XtextGenerator {
		configuration = {
			project = StandardProjectConfig {
				baseName = "de.unibonn.simpleml"
				rootPath = rootPath
				runtimeTest = {
					enabled = true
				}
				genericIde = {
					enabled = true
				}
			}
			code = {
				encoding = "UTF-8"
				lineDelimiter = "\r\n"
				fileHeader = "/*\n * generated by Xtext \${version}\n */"
				preferXtendStubs = false
			}
		}
		language = StandardLanguage {
			name = "de.unibonn.simpleml.SimpleML"
			fileExtensions = "simpleml"
			referencedResource = "platform:/resource/de.unibonn.simpleml/model/custom/SimpleML.genmodel"

//            formatter = {
//                generateStub = true
//            }
            generator = {
            	generateXtendMain = false
            }
			serializer = {
				generateStub = true
			}
			validator = {
				generateDeprecationValidation = true
			}
			junitSupport = {
				junitVersion = "5"
				generateStub = false
			}
		}
	}
}
